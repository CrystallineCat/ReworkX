// FIXME Case-insensitive keywords
// FIXME Quote escaping

@make_dict
query: 'MATCH' type=graph_type? traversal=path transformers=clause* EOF;

graph_type: 'GRAPH'
          | 'SUBGRAPH';

@collect_flat
path: path edge node
    | node;

@make_dict
node:  '(' node=content ')';

@make_dict
edge: '-[' edge=content ']-'
    | '-'  edge=empty_dict;

@make_dict
content: alias=ID ':' type=ID
       | alias=ID;

@key_value
clause: clause_type clause_params;

clause_type: 'TRANSFER'
           | 'PROJECT'
           | 'COMBINE';

@join_dicts
clause_params: pattern=path subclause+;

@key_value
subclause: 'DELETE' delete
         | 'SET'    set+[COMMA]
         | 'METHOD' method;

@make_dict
delete: alias=ID+[COMMA];

@make_dict
set: key=ID '='     value_lookup=LOOKUP
   | key=ID '=' '"' value=NOT_DOUBLE_QUOTE '"'
   | key=ID '=' "'" value=NOT_SINGLE_QUOTE "'";

@key_value
method: 'JACCARD' arguments
      | 'NEWMAN'  arguments
      | 'EDGES'   arguments
      | 'ATTRS'   no_arguments;

@make_dict
arguments: args=ID+[COMMA];

@make_dict
no_arguments: args=empty_dict;

@make_dict
empty_dict: EMPTY;


terminals
COMMA: ',';
ID: /(\w|\d|\_)+/;
LOOKUP: /(\w|\d|\_)+[.](\w|\d|\_)+/;
NOT_DOUBLE_QUOTE: /[^"]*/;
NOT_SINGLE_QUOTE: /[^']*/;
KEYWORD: /\w+/;
