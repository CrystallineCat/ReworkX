// FIXME Case-insensitive keywords
// FIXME Quote escaping
// FIXME Better joining of subclauses

@make_dict
query: 'MATCH' type=graph_type? traversal=path transformers=clause* EOF;

@make_keyword
graph_type: 'GRAPH'
          | 'SUBGRAPH';

@make_flat_list
path: path edge node
    | node;

@make_dict
node:  '(' node=alias ')';

@make_dict
edge: '-[' edge=alias ']-'
    | '-'  edge=empty_dict;

@make_dict
alias: alias=ID ':' type=ID
     | alias=ID;

@make_pair_dict_from_merged_dicts
clause: 'TRANSFER' pattern=path subclause+
      | 'PROJECT'  pattern=path subclause+
      | 'COMBINE'  pattern=path subclause+;

@make_pair_dict
subclause: 'DELETE' delete
         | 'SET'    set+[COMMA]
         | 'METHOD' method;

@make_dict
delete: alias=ID+[COMMA];

@make_dict
set: key=ID '='     value_lookup=LOOKUP
   | key=ID '=' '"' value=NOT_DOUBLE_QUOTE '"'
   | key=ID '=' "'" value=NOT_SINGLE_QUOTE "'";

@make_pair_dict
method: 'JACCARD' arguments
      | 'NEWMAN'  arguments
      | 'EDGES'   arguments
      | 'ATTRS'   no_arguments;

@make_dict
arguments: args=ID+[COMMA];

@make_dict
no_arguments: args=empty_dict;

@make_dict
empty_dict: EMPTY;


terminals
COMMA: ',';
ID: /(\w|\d|\_)+/;
LOOKUP: /(\w|\d|\_)+[.](\w|\d|\_)+/;
NOT_DOUBLE_QUOTE: /[^"]*/;
NOT_SINGLE_QUOTE: /[^']*/;
KEYWORD: /\w+/;
